// Version stuff for auto generated versionName and versionCode
def appVersionMajor = 1
def appVersionMinor = 2
def appVersionPatch = 0

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://jitpack.io" }
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        google()
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.jakewharton:butterknife-gradle-plugin:9.0.0-SNAPSHOT'
    }
}

repositories {
    // ...
    maven { url "https://jitpack.io" }
}

//
// +------------------------------------------------------------+
// | Configure parameters for the android build. Android DSL    |
// +------------------------------------------------------------+
// ||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
// Some App version stuff
if (new File('build_ver.gradle').exists()) { apply from: '../build_ver.gradle' }
// Local
if (new File('local.gradle').exists()) { apply from: '../local.gradle' }
// Dex stuff
if (new File('build_dex.gradle').exists()) { apply from: '../build_dex.gradle' }
apply plugin: 'com.jakewharton.butterknife'

repositories {
    // Snapshots
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    // Maven Central
    mavenCentral()

    google()

    // Jcenter is superset of Maven Central
    jcenter()

    // Project local
    flatDir {
        dirs 'libs'
    }

    // System local
    maven {
        url 'file:' + System.getenv('HOME') + '/.m2/repository'
    }

    // Fabric
    maven {
        url 'https://maven.fabric.io/public'
    }
}

android {
    compileSdkVersion compileSdkVersionExt

    defaultConfig {
        applicationId "org.brainail.EverboxingLexis"

        minSdkVersion 15
        targetSdkVersion 27

        versionCode appVersionMajor * 10000 + appVersionMinor * 100 + appVersionPatch
        versionName "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}"

        // Build config fields
        buildConfigField "String", "MODULE_NAME", "\"$project.name\""

        vectorDrawables.useSupportLibrary = true
    }

    flavorDimensions "default"

//    sourceSets {
//        // Remapping the androidTest sourceSet to the test folder
//        androidTest.setRoot('src/test')
//    }

    signingConfigs {
        release {
            try {
                // Load properties
                Properties signProperties = new Properties()
                signProperties.load(new FileInputStream(rootProject.file('sign/sign.properties')))

                // Assign properties
                storeFile file(signProperties.STORE_FILE)
                storePassword signProperties.STORE_PASSWORD
                keyAlias signProperties.KEY_ALIAS
                keyPassword signProperties.KEY_PASSWORD
            } catch (ignored) {
                throw new InvalidUserDataException("You should provide sign.properties file with correct signingConfigs!")
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"

            // Build config fields
            buildConfigField "boolean", "LOGGABLE", "true"

            // buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            // ext.enableCrashlytics = false
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true

            buildConfigField "boolean", "USE_LEAKCANARY", "false"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.txt'

            // Build config fields
            buildConfigField "boolean", "LOGGABLE", "false"

            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true

            buildConfigField "boolean", "USE_LEAKCANARY", "false"

            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        // ButterKnife
        disable 'InvalidPackage'
        // set to true to turn off analysis progress reporting by lint
        quiet false
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings false
        // if true, check all issues, including those that are off by default
        checkAllWarnings false
        // if true, treat all warnings as errors
        warningsAsErrors false
        // if true, generate an XML report for use by for example Jenkins
        xmlReport false
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        htmlReport true
        // optional path to report (default will be lint-results.html in the build dir)
        htmlOutput file("$project.buildDir/reports/lint/lint.html")
    }

    packagingOptions {
        // Common
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
        exclude 'NOTICE.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
        exclude '.readme'

        // ButterKnife
        exclude 'META-INF/services/javax.annotation.processing.Processor'

        // Slob
        project.file("slob_exclude.txt").eachLine {
            line ->
                // println line
                if (line && line.trim() && !line.startsWith("#")) {
                    exclude line.trim()
                }
        }
    }
}

//
// +------------------------------------------------------------+
// | Dependencies                                               |
// +------------------------------------------------------------+
// ||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

dependencies {
    implementation 'com.jakewharton:butterknife:9.0.0-SNAPSHOT'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-SNAPSHOT'
    // Libs folder
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // MultiDex
    implementation "com.android.support:multidex:${multiDexVersion}"
    // Support libraries
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:customtabs:${supportLibVersion}"
    implementation "com.android.support:support-annotations:${supportLibVersion}"
    // Google play services
    implementation "com.google.android.gms:play-services-base:${playServicesLibVersion}"
    implementation "com.google.android.gms:play-services-drive:${playServicesLibVersion}"
    implementation "com.google.android.gms:play-services-plus:${playServicesLibVersion}"
    // Material stuff
    implementation 'com.malinskiy:materialicons:1.0.2'
    implementation 'com.github.clans:fab:1.6.2'
    // Fonts
    implementation 'uk.co.chrisjenx:calligraphy:2.1.0'
    // UI stuff
    implementation 'com.github.iammert:ProgressLayout:a2ac196500'
    // Analytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.2.3@aar') { transitive = true; }
    // Slob stuff
    implementation 'com.ibm.icu:icu4j:53.1', 'org.tukaani:xz:1.5', 'com.fasterxml.jackson.core:jackson-databind:2.4.2'
    // About
    implementation('com.mikepenz:aboutlibraries:5.5.7@aar') { transitive = true }
    implementation 'com.github.paolorotolo:appintro:3.3.0'
    // Hardy dialogs
    implementation project(':EverboxingHardyDialogs')
    // Tools
    implementation project(':EverboxingTools')
    // Leaks
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${leakCanaryLibVersion}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryLibVersion}"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryLibVersion}"
}

//
// +------------------------------------------------------------+
// | Configure tests dependencies                               |
// +------------------------------------------------------------+
// ||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

// apply plugin: 'org.robolectric'

// To run through the Studio (previous approach)
// apply from: 'robolectric-iml.gradle'

configurations.all {
    resolutionStrategy {
        force "com.android.support:support-v4:${supportLibVersion}"
        force "com.android.support:appcompat-v7:${supportLibVersion}"
        force "com.android.support:support-compat:${supportLibVersion}"
    }
}

dependencies {
    // Tests
    testImplementation "junit:junit:4.12"
    testImplementation('org.robolectric:robolectric:3.1') {
        exclude module: 'commons-logging'
        exclude module: 'httpclient'

        // WARNING: Conflict with dependency 'com.ibm.icu:icu4j'.
        // Resolved versions for app (52.1) and test app (53.1) differ.
        // See http://g.co/androidstudio/app-test-app-conflict for details.
        exclude group: 'com.ibm.icu'
    }

    // Checks
    testImplementation ('com.squareup:fest-android:1.0.+') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
    testImplementation('com.squareup.assertj:assertj-android:1.1.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // Mocks
    testImplementation('org.powermock:powermock-module-junit4:1.6.4') {
        exclude group: 'org.hamcrest'
    }
    testImplementation('org.powermock:powermock-api-mockito:1.6.4') {
        exclude group: 'org.hamcrest'
    }
}

// project.gradle.taskGraph.whenReady {
//     testDebug {
//         ignoreFailures = true
//     }
// }

//
// +------------------------------------------------------------+
// | Configure analyzers                                        |
// +------------------------------------------------------------+
// ||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

// Add checkstyle, findbugs, pmd and lint to the check task.
task analyzecode(type: SourceTask) {
    description 'All checks for code'
}

analyzecode.dependsOn 'checkstyle', 'findbugs', 'pmd', 'lint'

//
// +------------------------------------------------------------+
// | Configure checkstyle                                       |
// +------------------------------------------------------------+
// ||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

apply plugin: 'checkstyle'

task checkstyle(type: Checkstyle) {
    description 'Check code style'
    ignoreFailures = true
    configFile file('config/checkstyle/checkstyle.xml')
    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

//
// +------------------------------------------------------------+
// | Configure findbugs                                         |
// +------------------------------------------------------------+
// ||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

apply plugin: "findbugs"

task findbugs(type: FindBugs) {
    description 'Find bugs'
    ignoreFailures = true
    source = fileTree('src/main/java/')
    classes = fileTree('build/intermediates/classes/debug/')
    classpath = files()
    effort = 'max'
    reportLevel = "high"
    include '**/*.java'
    exclude '**/gen/**'
    excludeFilter = new File('config/findbugs/findbugs-config.xml')
    reports {
        html.enabled = true
        xml.enabled = !html.enabled
    }
}

//
// +------------------------------------------------------------+
// | Configure pmd                                              |
// +------------------------------------------------------------+
// ||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

apply plugin: "pmd"

task pmd(type: Pmd) {
    description 'Analyze code via Pmd'
    ignoreFailures = true
    include '**/*.java'
    exclude '**/gen/**'
    source = fileTree('src/main/java/')
    ruleSetFiles = files("config/pmd/pmd-config.xml")
}
